<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009"
			   xmlns:s="library://ns.adobe.com/flex/spark"
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   width="300" height="150" title="编辑栏目窗口" 
			   close="closeWindowHandler(event)" creationComplete="initComplete(event)">
	<fx:Script>
		<![CDATA[
			import com.tang.common.Icons;
			import com.tang.common.SqliteConn;
			
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			[Bindable]
			public var chlName:String = "aa";
			//------------------------------------
			// 属性
			//------------------------------------
			private var _channelId:int;
			private var _dao:SqliteConn;
			private var _saveAfter:Function;

			public function get saveAfter():Function
			{
				return _saveAfter;
			}

			public function set saveAfter(value:Function):void
			{
				_saveAfter = value;
			}


			public function get dao():SqliteConn
			{
				return _dao;
			}

			public function set dao(value:SqliteConn):void
			{
				_dao = value;
			}


			public function get channelId():int
			{
				return _channelId;
			}

			public function set channelId(value:int):void
			{
				_channelId = value;
			}

			
			//------------------------------------
			// 事件处理
			//------------------------------------
			protected function initComplete(event:FlexEvent):void
			{
				this.channelName.setFocus();
			}
			
			protected function closeWindowHandler(event:CloseEvent):void
			{
				this.closeWin();
			}
			
			protected function saveBtnClickHandler(event:MouseEvent):void
			{
				if(this.channelName.text) {
					var sql:String = !this._channelId ? "insert into channels (channel_name, user_id) values (@channel_name, @user_id)" 
									: "update channels set channel_name=@channel_name where id=@channel_id";
					if(!_dao)
						_dao = new SqliteConn();
					var stmt:SQLStatement = _dao.createQuery(sql);
					stmt.parameters["@channel_name"] = this.channelName.text;
					if(this._channelId) {
						stmt.parameters["@channel_id"] = this._channelId;
					} else {
						stmt.parameters["@user_id"] = 1;
					}
					stmt.addEventListener(SQLEvent.RESULT, saveResult);
					stmt.addEventListener(SQLErrorEvent.ERROR, dao.errorHandler);
					stmt.execute();
				} else {
					Alert.show("请输入栏目名！", "温馨提示");
				}
			}
			
			protected function saveResult(event:SQLEvent):void {
				this.closeWin();
				if(saveAfter != null)
					saveAfter();
				Alert.show("保存成功！", "温馨提示");
			}
			
			protected function resetBtnClickHandler(event:MouseEvent):void
			{
				this.channelName.text = "";
			}
			
			//------------------------------------
			// 函数
			//------------------------------------
			public function closeWin():void {
				this._channelId = NaN;
				PopUpManager.removePopUp(this);
			}
			
			public static function show(fn:Function, 
										dao:SqliteConn, 
										 parent:DisplayObject = null, 
										 channelId:int = NaN, 
										 channelName:String = ""):ChannelWin {
				var win:ChannelWin = new ChannelWin();
				win.title = !channelId ? "新增栏目" : "编辑栏目";
				win.channelId = channelId;
				win.chlName = channelName;
				win.saveAfter = fn;
				PopUpManager.addPopUp(win, parent, true);
				PopUpManager.centerPopUp(win);
				return win;
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
		<mx:StringValidator id="cn" required="true" source="{channelName}"
							requiredFieldError="栏目名称不能为空" property="text" />
	</fx:Declarations>
	
	<s:Form left="2" right="2" top="2" bottom="2">
		<s:FormItem label="栏目名称：" required="true">
			<s:TextInput id="channelName" maxChars="100" text="{chlName}"/>
		</s:FormItem>
		<s:FormItem>
			<s:layout>
				<s:HorizontalLayout/>
			</s:layout>
			<s:Button label="保存" click="saveBtnClickHandler(event)" icon="{Icons.ICON_SAVE}" />
			<s:Button label="重置" click="resetBtnClickHandler(event)" icon="{Icons.ICON_RESET}" />
		</s:FormItem>
	</s:Form>
</s:TitleWindow>
